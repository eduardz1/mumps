@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-targets.cmake)

set(MUMPS_UPSTREAM_VERSION @MUMPS_UPSTREAM_VERSION@)
set(MUMPS_ACTUAL_VERSION @MUMPS_ACTUAL_VERSION@)

set(MUMPS_intsize64 @MUMPS_intsize64@)
set(MUMPS_parallel @MUMPS_parallel@)

set(MUMPS_LAPACK_VENDOR @LAPACK_VENDOR@)

set(MUMPS_SCALAPACK_VENDOR @SCALAPACK_VENDOR@)
set(MUMPS_scalapack @MUMPS_scalapack@)

set(MUMPS_s_FOUND @BUILD_SINGLE@)
set(MUMPS_SINGLE @BUILD_SINGLE@)

set(MUMPS_d_FOUND @BUILD_DOUBLE@)
set(MUMPS_DOUBLE @BUILD_DOUBLE@)

set(MUMPS_c_FOUND @BUILD_COMPLEX@)
set(MUMPS_COMPLEX @BUILD_COMPLEX@)

set(MUMPS_z_FOUND @BUILD_COMPLEX16@)
set(MUMPS_COMPLEX16 @BUILD_COMPLEX16@)

if(MUMPS_parallel)
  find_dependency(MPI COMPONENTS C Fortran)

  if(NOT MUMPS_LAPACK_VENDOR MATCHES "^MKL")
    find_dependency(LAPACK COMPONENTS ${MUMPS_LAPACK_VENDOR})
  endif()

  if(DEFINED MUMPS_SCALAPACK_VENDOR)
    find_dependency(SCALAPACK COMPONENTS ${MUMPS_SCALAPACK_VENDOR})
  elseif(MUMPS_scalapack)
    find_dependency(SCALAPACK)
  else()
    find_dependency(SCALAPACK CONFIG)
  endif()
else()
  find_dependency(LAPACK COMPONENTS ${MUMPS_LAPACK_VENDOR})

  # This is used in place of find_dependency(MPI) in MUMPS when MUMPS_parallel is OFF
  add_library(MPI::MPI_C INTERFACE IMPORTED)
  target_link_libraries(MPI::MPI_C INTERFACE MUMPS::mpiseq_c)

  add_library(MPI::MPI_Fortran INTERFACE IMPORTED)
  target_link_libraries(MPI::MPI_Fortran INTERFACE MUMPS::mpiseq_fortran)
endif()

set(MUMPS_scotch @MUMPS_scotch@)
if(MUMPS_scotch)
  find_dependency(SCOTCH CONFIG)
  if(MUMPS_parallel)
    target_link_libraries(SCOTCH::scotch INTERFACE SCOTCH::ptscotch SCOTCH::ptscotcherr SCOTCH::ptesmumps)
  endif()
  target_link_libraries(SCOTCH::scotch INTERFACE SCOTCH::scotcherr SCOTCH::esmumps)
endif()

set(MUMPS_metis @MUMPS_metis@)
if(MUMPS_metis)
  find_dependency(metis CONFIG)
endif()

set(MUMPS_parmetis @MUMPS_parmetis@)
if(MUMPS_parmetis)
  find_dependency(parmetis CONFIG)
endif()

set(MUMPS_openmp @MUMPS_openmp@)
if(MUMPS_openmp)
  find_dependency(OpenMP COMPONENTS C Fortran)
endif()

check_required_components(@PROJECT_NAME@)
